plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
}

def env = new Properties()
file(".env").withInputStream {
    env.load(it)
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    withJavadocJar()
    withSourcesJar()
}

application {
    mainClass = 'de.craftsblock.craftsnet.CraftsNet'
}

group = 'de.craftsblock'
version = '3.4.3-pre3'

repositories {
    mavenCentral()
    maven { url "https://repo.craftsblock.de/releases" }
}

dependencies {
    // CraftsBlock related dependencies ---------------------------------------------------------------------------------------

    // https://repo.craftsblock.de/#/releases/de/craftsblock/craftscore/bom
    implementation platform("de.craftsblock.craftscore:bom:3.8.6")

    // https://repo.craftsblock.de/#/releases/de/craftsblock/craftscore/utils
    implementation "de.craftsblock.craftscore:utils"

    // https://repo.craftsblock.de/#/releases/de/craftsblock/craftscore/cache
    implementation "de.craftsblock.craftscore:cache"

    // https://repo.craftsblock.de/#/releases/de/craftsblock/craftscore/event
    implementation "de.craftsblock.craftscore:event"

    // https://repo.craftsblock.de/#/releases/de/craftsblock/craftscore/json
    implementation "de.craftsblock.craftscore:json"

    // Third party needed dependencies ----------------------------------------------------------------------------------------

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation 'com.google.code.gson:gson:2.13.1'

    // https://mvnrepository.com/artifact/org.apache.tika/tika-core
    implementation 'org.apache.tika:tika-core:3.1.0'

    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    implementation 'org.jetbrains:annotations:26.0.2'

    // Artifact resolving dependencies ----------------------------------------------------------------------------------------

    // https://mvnrepository.com/artifact/org.apache.maven/maven-resolver-provider
    // Stay with this version as there is no update available for the maven-resolver-supplier.
    implementation 'org.apache.maven:maven-resolver-provider:4.0.0-alpha-13'

    // https://mvnrepository.com/artifact/org.apache.maven.resolver/maven-resolver-supplier
    implementation 'org.apache.maven.resolver:maven-resolver-supplier:2.0.0-alpha-8'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClass.get()
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        normal(MavenPublication) {
            artifactId "craftsnet"
            from components.java
            pom {
                name = 'CraftsNet'
                description = 'Easy creation of HTTP routes and WebSocket endpoints in Java.'

                scm {
                    url = 'https://github.com/CraftsBlock/CraftsNet'
                    connection = 'scm:git:git://github.com/CraftsBlock/CraftsNet.git'
                    developerConnection = 'scm:git:git@github.com:CraftsBlock/CraftsNet.git'
                }

                issueManagement {
                    system = 'github'
                    url = 'https://github.com/CraftsBlock/CraftsNet/issues'
                }

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://github.com/CraftsBlock/CraftsNet/blob/master/LICENSE'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url('https://repo.craftsblock.de/experimental')
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = env["username"]
                password = env["password"]
            }
        }
    }
}
